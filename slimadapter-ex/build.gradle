plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "com.github.dcendents.android-maven" version "1.5"
}
apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 9
        versionName "2.0.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

group = 'net.idik'
version = '2.0.2'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def siteUrl = 'https://github.com/MEiDIK/SlimAdapter'
def gitUrl = 'https://github.com/MEiDIK/SlimAdapter.git'

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'SlimAdapter'
        name = 'SlimAdapterEx'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        userOrg = 'idik-net'
        publish = true

        version {
            name = '2.0.2'
            desc = 'New Feature: SlimAdapterEx'
            released = new Date()
            vcsTag = 'v2.0.2'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}

repositories {
    mavenCentral()
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'SlimAdapterEx'
                description 'SlimAdapterEx Module'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/MEiDIK/SlimAdapter/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'iDIK'
                        name 'iDIK'
                        email 'me@idik.net'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
//    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    provided project(':slimadapter')
    testCompile 'junit:junit:4.12'
}
